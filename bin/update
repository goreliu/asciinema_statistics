#!/bin/zsh

cd ${0:h}/..

export LANG=C

update_list() {
    # update data/list.txt

    sort data/list.txt > last.txt

    echo '[list][begin]'

    # max page number in https://asciinema.org/explore/public
    # no update needed
    for i ({1..1759}) {
        echo "process page $i ..."

        curl "https://asciinema.org/explore/public?order=date&page=$i" 2>/dev/null \
            | grep 'href="/a/' | sed 's|.*href="/a/||g; s|".*||g' \
            | sort -u > append.txt

        touch append_old.txt

        if [[ -n $(comm -12 append.txt last.txt) ]] {
            echo -n "$(cat last.txt | wc -l) -> "

            {comm -23 append.txt last.txt; cat append_old.txt} | sort -un >> data/list.txt

            cat data/list.txt | wc -l
            break
        } else {
            cat append_old.txt >> append.txt
            mv append.txt append_old.txt
        }
    }

    echo '[list][end]'

    rm append*.txt last.txt
}

update_statistics() {
    # update data/{os.txt,shell.txt,term.txt,user.txt}

    # 0: no update
    # 1: error
    # 2: update success
    local ret=0

    [[ -d pages ]] || mkdir pages

    local begin=0
    [[ -f data/os.txt ]] && {
        begin=${$(grep -n "^$(tail -n 1 data/os.txt | cut -f1 -d'@')$" data/list.txt)%:*}
    }

    echo '[statistics][begin]'

    for i ($(sed -n "$((begin + 1)),\$p" data/list.txt)) {
        echo "process cast $i ..."
        [[ -f pages/$i ]] || curl "https://asciinema.org/a/$i" -o pages/$i 2> /dev/null

        local os=$(grep -F '<code>OS=' pages/$i | sed 's/^.*=//g; s/<.*$//g')
        local shell=$(grep -F '<code>SHELL=' pages/$i | sed 's/^.*=//g; s/<.*$//g; s/\\//g')
        local term=$(grep -F '<code>TERM=' pages/$i | sed 's/^.*=//g; s/<.*$//g')
        local user=$(grep -o 'player author="[^"]*"' pages/$i | cut -f2 -d'"')

        case $os {
            (*linux)
            os='Linux'
            ;;

            (*macos|*macOS)
            os='macOS'
            ;;

            (freebsd)
            os='FreeBSD'
            ;;

            (*windows)
            os='Windows'
            ;;

            (android)
            os='Android'
            ;;
        }

        case $shell {
            (*bash*)
            shell='bash'
            ;;

            (*zsh*)
            shell='zsh'
            ;;

            (*cmd.exe*)
            shell='cmd.exe'
            ;;
        }

        if [[ -n $os$shell$term$user ]] {
            echo $i@${os:-Unknown} >> data/os.txt
            echo $i@${shell:-unknown} >> data/shell.txt
            echo $i@${term:-unknown} >> data/term.txt
            echo $i@${user:-unknown} >> data/user.txt
        } else {
            echo error
            return 1
        }

        [[ -d casts && ! -f casts/$i.cast.gz ]] && {
            curl -L "https://asciinema.org/a/$i.cast?dl=1" -o casts/$i.cast.gz 2> /dev/null || {
                echo error
                return 1
            }
        }

        ret=2
    }

    echo '[statistics][end]'
    return $ret
}

update_list
update_statistics

if (($? == 2)) {
    echo '[generate][begin]'
    ./bin/generate && echo '[generate][end]'
}
